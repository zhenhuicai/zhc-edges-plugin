apply plugin: 'maven-publish'

jar {
    afterEvaluate {
        manifest {
            attributes["Implementation-Title"] = project.name
            attributes["Implementation-Version"] = project.version
            attributes["Automatic-Module-Name"] = project.name.replace('-', '.')  // for Jigsaw
            attributes["Created-By"] = "${System.getProperty("java.version")} (${System.getProperty("java.specification.vendor")})"
            attributes["Created-At"] = new Date().format("yyyy-MM-dd HH:mm:ss z")
        }
    }
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

javadoc {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        failOnError false
        links "https://docs.oracle.com/javase/17/docs/api"
    }
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from 'build/docs/javadoc'
}

artifacts {
    archives jar
    //no source any more
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {

            artifactId = "${project.name}"
            from components.java

            artifact sourcesJar
            artifact javadocJar

            pom {
                // POM_NAME
                name = "${project.name}"
                // POM_DESCRIPTION
                description = "${project.description}"
                url = GIT_URL

                scm {
                    connection = GIT_SCM_CONNECTION
                    developerConnection = GIT_SCM_CONNECTION
                    url = GIT_URL
                }
                licenses {
                    license {
                        name = LICENSE_NAME
                        url = LICENSE_URL
                    }
                }
                developers {
                    developer {
                        name = DEVELOPER_NAME
                        email = DEVELOPER_EMAIL
                    }
                }

                withXml {
                    def root = asNode()
                    root.dependencies.'*'.findAll {
                        def d = it
                        d.scope.text() == 'runtime' && project.configurations
                                .findByName("implementation").allDependencies.find { dep ->
                            dep.name == it.artifactId.text()
                        }.each() {
                            d.scope*.value = 'compile'
                            d.appendNode('optional', true)
                        }
                    }
                }
            }
        }
    }
}

jreleaser {
    project {
        name = rootProject.name
        version = rootProject.version
        description = rootProject.description
        group = 'io.github.zhenhuicai'
        inceptionYear = '2024'
        java {
            groupId = 'io.github.zhenhuicai'
            artifactId = project.name
            version = rootProject.version
        }
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
            /* Legacy OSSRH
            nexus2 {
                'maven-central' {
                    active = 'ALWAYS'
                    url = 'https://s01.oss.sonatype.org/service/local'
                    snapshotUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                    closeRepository = true
                    releaseRepository = true
                    stagingRepository('build/staging-deploy')
                }
            }
            */
        }
    }
    signing {
        pgp {
            active = 'ALWAYS'
            armored = true
        }
    }
}