apply plugin: "java-library"
apply plugin: "hope.optional"

apply from: "${rootProject.projectDir}/gradle/java.gradle"

//This for pure spring extension, not Boot application
//用于spring lib 扩展包，非应用项目

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << "-parameters"
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    management {
        canBeConsumed = false
        canBeResolved = false
        visible = false
    }
    compileClasspath.extendsFrom(management)
    testCompileClasspath.extendsFrom(management)
    runtimeClasspath.extendsFrom(management)
    testRuntimeClasspath.extendsFrom(management)
    annotationProcessor.extendsFrom(management)
    testAnnotationProcessor.extendsFrom(management)
}

dependencies {

    management platform(libs.spring.boot.dependencies)

    //Spring stuff
    annotationProcessor(libs.spring.boot.configuration.processor)
    implementation(libs.spring.boot.autoconfigure)

    //Test stuff
    testImplementation(libs.spring.boot.starter.test)
    testRuntimeOnly(libs.junit.platform.launcher)
}


tasks.named('test') {
    useJUnitPlatform()
}
